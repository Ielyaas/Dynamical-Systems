# Bifurcation analysis of HEK model

#  Initial conditions for the steady state, p as a parameter

init c=0.078
init ce=10
init h=0.525273


# Parameter values

param p=0
param alpha1=0.01

param gamma=5.5
param delta=2.5

param Kc=0.2
param Kp=0.2
param Kf=10
param Kb=0.4
param Kh=0.08
param tau_max=20
param Ktau=0.1

param Vs=0.9
param Kbar=1.957e-5
param Ks=0.2
param Kce=8
param Vpm=0.11
param Kpm=0.3


# Functions of the model

phi_c=c^4/(c^4+Kc^4)
phi_p=p^2/(Kp^2+p^2)
phi_p_down=Kp^2/(Kp^2+p^2)
h_inf=Kh^4/(Kh^4+c^4)
tau = tau_max*Ktau^4/(Ktau^4+c^4)

beta = phi_p*phi_c*h
alpha = phi_p_down*(1-phi_c*h_inf)

Po=beta/(beta + Kb*(beta+alpha))

Jipr=Kf*Po*(ce-c)
Jpm = Vpm*c^2/(Kpm^2+c^2)
Jin = alpha1*(Kce^2/(Kce^2+ce^2))
#alpha_orai = alpha1/(1+K_orai(alpha2+alpha3))+alpha2+alpha3
#Jin = alpha_orai*(Kce^2/(Kce^2+ce^2)) 
Jserca=Vs*(c^2-Kbar*ce^2)/(c^2+Ks^2)

# Functions of interest

aux IPR = Jipr
aux IN = Jin

# Differential Equations

dc/dt=Jipr-Jserca+delta*(Jin-Jpm)
dce/dt=gamma*(Jserca-Jipr)
dh/dt=(h_inf-h)/tau

# Continuation parameters

@ total=400,dt=0.05,xp=t,yp=c,xlo=0,xhi=400,ylo=0,yhi=0.6,meth=gear,bounds=1000
@ ntst=300,dsmin=0.000001,ds=0.0001,dsmax=0.01,parmax=2
@ nmax=50000, npr=5000
@ autoxmin=0,autoxmax=2,autoymin=0,autoymax=2

done
